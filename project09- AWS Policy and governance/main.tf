resource "random_string" "random" {
  length  = 6
  special = false
  upper   = false
}

resource "aws_s3_bucket" "config_bucket" {
  bucket = "${var.project_name}-twuptea-${random_string.random.result}"
  force_destroy = true

  tags = {
    Name        = "${var.project_name}-twuptea-${random_string.random.result}"
    Environment = "governance"
    Purpose     = "Config Bucket"
  }
}

#enable versioning on config bucket
resource "aws_s3_bucket_versioning" "config_bucket_versioning" {
  bucket = aws_s3_bucket.config_bucket.id
  versioning_configuration {
    status = "Enabled"
  }
}

#enable encryption on config bucket
resource "aws_s3_bucket_server_side_encryption_configuration" "config_bucket_encryption" {
  bucket = aws_s3_bucket.config_bucket.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

#block public access on config bucket
resource "aws_s3_bucket_public_access_block" "config_bucket_public_access_block" {
  bucket = aws_s3_bucket.config_bucket.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

#s3 bucket policy for config

resource "aws_s3_bucket_policy" "config_bucket_policy" {
  bucket = aws_s3_bucket.config_bucket.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid = "AWSConfigBucketPermissionsCheck"
        Effect = "Allow"
        Principal = {
            Service = "config.amazonaws.com"
        }
        Action = "s3:GetBucketAcl"
        Resource = aws_s3_bucket.config_bucket.arn
      },


      {
        Sid = "AWSConfigBucketExistenceCheck"
        Effect = "Allow"
        Principal = {
            Service = "config.amazonaws.com"
        }
        Action = "s3:ListBucket"
        Resource = aws_s3_bucket.config_bucket.arn
      },

      {
        Sid = "AWSConfigBucketPutObject"
        Effect = "Allow"
        Principal = {
            Service = "config.amazonaws.com"
        }
        Action = "s3:PutObject"
        Resource = "${aws_s3_bucket.config_bucket.arn}/*"
        Condition = {
          StringEquals = {
            "s3:x-amz-acl" = "bucket-owner-full-control"
          }
        }

      },
      {

        Sid = "DenyInsecureTransport"
        Effect = "Deny"
        Principal = "*"

        Action = "s3:*"
        Resource = [aws_s3_bucket.config_bucket.arn, "${aws_s3_bucket.config_bucket.arn}/*"]
        Condition = {
          Bool = {
            "aws:SecureTransport" = false
          }
        }
      }

    ]
  })
  depends_on = [aws_s3_bucket_public_access_block.config_bucket_public_access_block]
}